#!@WHICHPERL@
=head1 NAME

momo_webservice - Run MoMo

=head1 SYNOPSIS

momo_webservice [options] [-ptm <ptm file>]+
                          [ -filter-field <encoded field name> 
                            -filter-type lt|le|eq|ge|gt 
                            -filter-thresh <threshold> ] 

    [-ptm <ptm file>]+                    Specify a PTM file for processing
    -psm-type <type>			  comet|ms-gf+|tide|percolator
    -psm-column-name <name>		  Name of column containing modified peptides
					  (PTM file(s) must be in PSM format)
    [-filter-field <encoded field name>    Specify a field of the previously
                                           specified PTM file to filter on.
    -filter-type lt|le|eq|ge|gt           Specify the filtering type for the
                                           previously specified PTM file as: 
                                            lt   - less than,
                                            le   - less than or equal,
                                            eq   - equal,
                                            ge   - greater than or equal, or
                                            gt   - greater than.
    -filter-thresh <threshold>]            Specify the number used to filter the
                                           previously specified PTM file.
    -flank <FASTA file>                   Specify a protein FASTA file
                                           containing flanking sequence;
                                           any paths begining with db/ are
                                           pre-existing databases.
    -occurs <num>                         Set the minimum number of occurences.
    -width <width>                        Set the width of the motifs.
    -remove-unknowns                      modified peptides will be removed if 
                                            they contain 'X'
    -single-per-mass                      A single motif will be generated for each distinct
                                            modification mass.
    -harvard				  emulate original motif-x (including p-value hack)
    -help                                 Show this brief help message.

=cut

use strict;
use warnings;
# load standard perl libraries
use Getopt::Long qw(:config permute auto_help);
use Pod::Usage;
# load custom perl libraries
use lib qw(@PERLLIBDIR@);
use StatusPage qw(arg_checks arg_end parse_uploaded opt_db_or_uploaded opt_choice opt_encoded opt_integer opt_number);

# constants
my $bin_dir = '@BINDIR@';
my $fasta_db_dir = '@MEMEDB@/fasta_databases';
# required parameters
my $algorithm;
my @ptms = (); # list of PTM objects with optional filters
my $psm_type;
my $psm_column_name;
my $width;
my $flank_file;
# option defaults
my ($filter_field, $filter_type, $filter_thresh);
my $remove_unknowns;
my $eliminate_width = 0; # don't eliminate repeats
my $occurs;
my $single_per_mass = 0; # FALSE
my $harvard = 0;	 # FALSE
my ($score_threshold, $max_motifs, $max_iterations, $max_no_decrease);
#status page
my $status = new StatusPage('MOMO', \@ARGV);
$status->add_message('Parsing arguments');
# parse options
my @arg_errors = ();
my $opts_ok = do {
  local $SIG{__WARN__} = sub { my ($msg) = @_; chomp($msg); push(@arg_errors, $msg); };
  GetOptions(
  '<>' => arg_checks(arg_end()),
  'algorithm=s' => opt_choice(\$algorithm, 'alg_simp', 'alg_mtfx', 'alg_modl'),
  'ptm=s' => sub {
    my ($opt, $value) = @_; my $file;
    parse_uploaded($opt, $value, \$file);
    push(@ptms, {file => $file});
  },
  'psm-type=s' => opt_encoded(\$psm_type),
  'psm-column-name=s' => opt_encoded(\$psm_column_name),
  'width=i' => opt_integer(\$width, 1, 51),
  'flank=s' => opt_db_or_uploaded(\$flank_file, $fasta_db_dir, 'db'),
  'filter-field=s' => opt_encoded(\$filter_field),
  'filter-type=s' => opt_choice(\$filter_type, 'lt', 'le', 'eq', 'ge', 'gt'),
  #'filter-thresh=f' => opt_number(\$filter_thresh, '>', 0),
  'filter-thresh=f' => opt_number(\$filter_thresh),
  'remove-unknowns=s' => opt_choice(\$remove_unknowns, 'T', 'F'),
  'eliminate-width=i' => opt_integer(\$eliminate_width, 0, 51),
  'occurs=i' => opt_integer(\$occurs, 1),
  'score-threshold=f' => opt_number(\$score_threshold, '<', 1),
  'score-threshold=f' => opt_number(\$score_threshold, '>', 0),
  'max-motifs=i' => opt_integer(\$max_motifs, 1, 1000),
  'max-iterations=i' => opt_integer(\$max_iterations, 1, 1000),
  'max-no-decrease=i' => opt_integer(\$max_no_decrease, 1, 1000),
  'single-per-mass' => \$single_per_mass,
  'harvard' => \$harvard
  );
};
# add additional error messages for missing sequences and motifs
push(@arg_errors, "No PTM files provided.") unless @ptms;
for (my $i = 0; $i < scalar(@ptms); $i++) {
  my $ptm = $ptms[$i];
#  if (defined($ptm->{field}) || defined($ptm->{type}) || defined($ptm->{thresh})) {
#    unless (defined($ptm->{field}) && defined($ptm->{type}) && defined($ptm->{thresh})) {
#      push(@arg_errors, "Missing filter fields for \"" . $ptm->{file} . "\".");
#    }
#  }
}
# display the error messages both to the status page and stdout
foreach my $arg_error (@arg_errors) {
  print STDERR $arg_error, "\n";
  $status->add_message($arg_error);
}
$opts_ok = 0 if (scalar(@arg_errors) > 0);
# setup status page

$status->add_file('html', 'momo.html', 'MOMO HTML output');
$status->add_file('txt', 'momo.txt', 'MOMO TXT output');

for (my $i = 0; $i < scalar(@ptms); $i++) {
  $status->add_file('ptm'.$i, $ptms[$i]->{file}, 'Post-translational modifications (PTM) file ' . ($i + 1));
}
$status->add_message($opts_ok ? 'Arguments ok' : "Error parsing arguments");
$status->update($opts_ok ? 'Starting' : 'Error');
# exit if there was an error reading the arguments
unless ($opts_ok) {
  $status->write_log();
  pod2usage(2);
}
# create the symlink to the databases
symlink($fasta_db_dir, 'db');
# ensure it will be removed on completion (when the log is written)
my @cleanup_files = ('db');
$status->set_cleanup(
  sub {
    foreach my $file (@cleanup_files) {
      unlink($file);
    }
  }
);

my $alg_string;
$alg_string = "simple" if ($algorithm eq 'alg_simp');
$alg_string = "motifx" if ($algorithm eq 'alg_mtfx');
$alg_string = "modl" if ($algorithm eq 'alg_modl');

my @momo_args = ($alg_string, '-oc', '.', '--verbosity', 1);
push(@momo_args, '--psm-type', $psm_type) if (defined($psm_type));
push(@momo_args, '--sequence-column', $psm_column_name) if (defined($psm_column_name));
push(@momo_args, '--width', $width);
push(@momo_args, '--protein-database', $flank_file) if (defined($flank_file));
if (defined($filter_field) && defined($filter_type) && defined($filter_thresh)) {
  my $filter_string = "$filter_field,$filter_type,$filter_thresh";
  push(@momo_args, '--filter', $filter_string) 
}
push(@momo_args, '--eliminate-repeats', $eliminate_width);
push(@momo_args, '--remove-unknowns', $remove_unknowns) if (defined($remove_unknowns));
push(@momo_args, '--min-occurrences', $occurs) if (defined($occurs));
push(@momo_args, '--single-motif-per-mass') if ($single_per_mass != 0);
push(@momo_args, '--harvard') if ($harvard != 0);
push(@momo_args, '--score-threshold', $score_threshold) if (defined($score_threshold));
push(@momo_args, '--max-motifs', $max_motifs) if (defined($max_motifs));
push(@momo_args, '--max-iterations', $max_iterations) if (defined($max_iterations));
push(@momo_args, '--max-no-decrease', $max_no_decrease) if (defined($max_no_decrease));
for (my $i = 0; $i < scalar(@ptms); $i++) {
  push(@momo_args, ($ptms[$i])->{file});
}
$status->run(PROG => 'momo', BIN => $bin_dir, ARGS => \@momo_args);

# done
$status->add_message("Done");
$status->update();
$status->write_log();
1;
